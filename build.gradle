buildscript {
	repositories {
		maven { url 'https://maven.minecraftforge.net' }
		maven { url 'https://repo.spongepowered.org/repository/maven-public' }
		mavenCentral()
		gradlePluginPortal()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '6.0.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7.+'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

def coreMixin = 'mixins.liteloader.core.json'
def coreRefMap = 'mixins.liteloader.core.refmap.json'

def clientMixin = 'mixins.liteloader.client.json'
def clientRefMap = 'mixins.liteloader.client.refmap.json'

configurations {
	embed
	implementation.extendsFrom(embed)
	clientAnnotationProcessor
}

minecraft {
	mappings channel: 'stable', version: '39-1.12'

	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
			args += ['--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
					'--mixin', coreMixin,
					'--mixin', clientMixin]

			mods {
				sanctum {
					source sourceSets.main
				}
			}
		}

		server {
			workingDirectory project.file('run')
			property 'forge.logging.markers', 'REGISTRIES'
			property 'forge.logging.console.level', 'debug'
			args += ['--tweakClass', 'org.spongepowered.asm.launch.MixinTweaker',
					'--mixin', coreMixin,
					'nogui']

			mods {
				sanctum {
					source sourceSets.main
				}
			}
		}
	}
}

ext {
	def isJenkins = project.hasProperty('jenkins')

	buildVersion = project.hasProperty('buildVersion') ? buildVersion : '0.0'
	isReleaseBuild = true
	brand = isJenkins ? "${project.mcVersion}-SNAPSHOT-r${System.env.GIT_COMMIT.take(7).toUpperCase()}-b${System.env.BUILD_NUMBER}-${System.env.BUILD_ID}" : ''

	projectName = 'LiteLoader'
	inceptionYear = '2012'
	packaging = 'jar'

	startClass = 'com.mumfrey.liteloader.debug.Start'
	tweakClass = 'com.mumfrey.liteloader.launch.LiteLoaderTweaker'
}

group = 'com.mumfrey'
archivesBaseName = 'liteloader'
version = buildVersion

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url 'https://repo.spongepowered.org/repository/maven-public/'
	}

	maven {
		url "https://maven.outlands.top/releases/"
	}
}

dependencies {
	minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2860'
	compileOnly fileTree(include: ['*.jar'], dir: 'lib')
	implementation 'top.outlands:foundation:0.13.11'
	implementation 'org.spongepowered:mixin:0.8.7'

	annotationProcessor 'org.spongepowered:mixin:0.8.7:processor'
	clientAnnotationProcessor 'org.spongepowered:mixin:0.8.7:processor'
}

sourceSets {
	main {
		ext.refMap = coreRefMap
		resources.srcDir 'src/generated/resources'
	}
	client {
		compileClasspath += main.compileClasspath + main.output
		resources.srcDir 'src/generated/resources'
		ext.refMap = clientRefMap
	}
	debug {
		compileClasspath += client.compileClasspath + client.output
	}
}

mixin {
	add sourceSets.main, coreRefMap
	add sourceSets.client, clientRefMap

	config coreMixin
	config clientMixin
}

compileClientJava {
	options.annotationProcessorPath = configurations.clientAnnotationProcessor
}

afterEvaluate {
	logger.lifecycle '================================================='
	logger.lifecycle '  LiteLoader'
	logger.lifecycle '  Copyright (C) 2011-2017 Adam Mummery-Smith'
	logger.lifecycle '  Running in {} mode', (project.isReleaseBuild ? 'RELEASE' : 'SNAPSHOT')
	logger.lifecycle '================================================='
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	inputs.property 'brand', project.brand
	from (sourceSets.main.resources.srcDirs) {
		include 'liteloader.properties'
		filter { line -> line.startsWith('brand=') ? line + project.brand : line }
	}
}

def jarManifest = {
	attributes (
		'Built-By': System.properties['user.name'],
		'Created-By': "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
		'Implementation-Title': name,
		'Implementation-Version': version,
		'Implementation-Vendor': url,
		'TweakClass': tweakClass
	)
}

jar {
	doFirst {
		ant.replace(
			file: file("${compileJava.temporaryDir}/${sourceSets.main.refMap}"),
			token: "func_72355_a(Lnet/minecraft/network/NetworkManager;Lnet/minecraft/entity/player/EntityPlayerMP;)V",
			value: "initializeConnectionToPlayer(Lnet/minecraft/network/NetworkManager;Lnet/minecraft/entity/player/EntityPlayerMP;Lnet/minecraft/network/NetHandlerPlayServer;)V"
		)
	}

	from sourceSets.client.output
	from sourceSets.debug.output
	manifest jarManifest
}

tasks.withType(JavaCompile).configureEach {
	options.deprecation = true
	options.encoding = 'utf8'
	options.release = 21

	options.compilerArgs += [
		'-Xlint:all',
		'-Xlint:-path',
		'-Xlint:-rawtypes',
		'-Xlint:-processing'
	]
}

artifacts {
	archives jar
}
